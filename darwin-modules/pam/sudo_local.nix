# This module applies only starting with Sonoma (14.5)
#
# PAM (Pluggable Authentication Modules) Configuration for sudo_local
#
# macOS 14.5+ introduced /etc/pam.d/sudo_local as a way to customize sudo authentication
# without modifying the system-managed /etc/pam.d/sudo file.
#
# This module provides a declarative interface for managing PAM entries in sudo_local.
# Each entry represents a PAM module invocation with the standard PAM configuration format:
#   type    control    module    arguments    # comment
#
# Example usage:
#   security.pam.sudo_local.entries = [
#     {
#       type = "auth";
#       control = "sufficient";
#       module = "pam_tid.so";  # Touch ID authentication
#       comment = "Enable Touch ID for sudo";
#     }
#   ];
#
# The module handles:
# - Version checking (requires macOS 14.5+)
# - Proper PAM entry formatting (tab-separated fields)
# - Automatic comment generation for nix-darwin tracking
{
  config,
  lib,
  ...
}:
with lib // config.versions.darwin.lib;
let
  cfg = config.security.pam.sudo_local;
in
{
  options.security.pam.sudo_local = {
    entries = mkOption {
      type = types.listOf (
        types.submodule {
          options = {
            type = mkOption {
              type = types.enum [
                "auth"
                "account"
                "password"
                "session"
              ];
              default = "auth";
            };
            control = mkOption {
              type = types.enum [
                "required"
                "requisite"
                "sufficient"
                "optional"
                "include"
                "substack"
              ];
              default = "optional";
            };
            module = mkOption {
              type = types.str;
            };
            arguments = mkOption {
              type = types.listOf types.str;
              default = [ ];
            };
            comment = mkOption {
              type = types.str;
              default = "security.pam.sudo_local";
            };
          };
        }
      );
    };
  };

  config = {
    assertions = [
      {
        assertion = versionAtLeast "14.5";
        message = "security.pam.sudo_local requires MacOS Sonoma (14.5)";
      }
    ];

    environment.etc."pam.d/sudo_local".text =
      let
        formatEntry =
          entry:
          lib.concatStringsSep "\t" [
            entry.type
            entry.control
            entry.module
            (concatStringsSep " " entry.arguments)
            "# nix-darwin: ${entry.comment}"
          ];
        lines = [
          "# Generated by nix-darwin sudo_local module"
        ]
        ++ (builtins.map formatEntry cfg.entries);
      in
      lib.concatStringsSep "\n" lines;
  };
}
